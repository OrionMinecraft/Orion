description = 'OrionLauncher'

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'application'

jar {
    baseName = "${project.description}"
    manifest {
        attributes(
                'Implementation-Version': version,
                'Orion-Git-Hash': gitHash
        )
    }
}

shadowJar {
    baseName = "${project.description}-shaded"
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
    dependencies {
        // Tools
        include dependency('eu.mikroskeem:shuriken.common')
        include dependency('eu.mikroskeem:shuriken.instrumentation')
        include dependency('eu.mikroskeem:shuriken.reflect')

        // Dependency downloader
        include dependency('eu.mikroskeem:picomaven')
        include dependency('com.squareup.okhttp3:okhttp')
        include dependency('com.squareup.okio:okio')
        include dependency('org.apache.maven:maven-repository-metadata')
        include dependency('org.codehaus.plexus:plexus-utils')

        // Logging
        include dependency('org.slf4j:slf4j-api')
        include dependency('ch.qos.logback:logback-classic')
        include dependency('ch.qos.logback:logback-core')

        // Orion
        include project(':orion.mixins')
        include project(':orion.core')
        include project(':orion.api')
    }
}

task copyNormal(type: Copy, dependsOn: [jar]) {
    from jar
    into "build/libs"
    rename { String fileName ->
        fileName.replace(jar.getArchiveName(), "${project.description}.jar")
    }
}

task copyShaded(type: Copy, dependsOn: [shadowJar]) {
    from shadowJar
    into "build/libs"
    rename { String fileName ->
        fileName.replace(shadowJar.getArchiveName(), "${project.description}-shaded.jar")
    }
}

task buildOrionLauncher(type: Task, dependsOn: [copyNormal, copyShaded]) {}

dependencies {
    compile project(':orion.mixins')
    compile project(':orion.core')
    compile project(':orion.api')

    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile(group: 'eu.mikroskeem', name: 'shuriken.instrumentation', version: '0.0.1-SNAPSHOT') {
        exclude(module: 'asm-all')
    }

    compileOnly group: 'net.minecraft', name: 'launchwrapper', version: '1.13-mikroskeem'
}

build.dependsOn buildOrionLauncher

// Launcher info
applicationName = "OrionLauncher"
mainClassName = "eu.mikroskeem.orion.launcher.Bootstrap"