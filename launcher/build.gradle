description = 'OrionLauncher'

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'application'

jar {
    baseName = "${project.description}"
    manifest {
        attributes(
                'Implementation-Version': version,
                'Orion-Git-Hash': gitHash
        )
    }
}

shadowJar {
    baseName = "${project.description}-shaded"
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
    dependencies {
        exclude dependency('com.google.gson:gson')
        exclude dependency("com.google.guava:guava")
        exclude dependency("ninja.leaping.configurate:configurate-core")
        exclude dependency("ninja.leaping.configurate:configurate-hocon")
        exclude dependency("com.typesafe:config")
        exclude dependency('org.ow2.asm:asm-all')
    }
}

task copyNormal(type: Copy, dependsOn: [jar]) {
    from jar
    into "build/libs"
    rename { String fileName ->
        fileName.replace(jar.getArchiveName(), "${project.description}.jar")
    }
}

task copyShaded(type: Copy, dependsOn: [shadowJar]) {
    from shadowJar
    into "build/libs"
    rename { String fileName ->
        fileName.replace(shadowJar.getArchiveName(), "${project.description}-shaded.jar")
    }
}

task buildOrionLauncher(type: Task, dependsOn: [copyNormal, copyShaded]) {}

dependencies {
    compile project(':orion.mixins')
    compile project(':orion.core')
    compile project(':orion.api')

    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile(group: 'eu.mikroskeem', name: 'shuriken.instrumentation', version: '0.0.1-SNAPSHOT') {
        exclude(module: 'asm-all')
    }

    compileOnly group: 'net.minecraft', name: 'launchwrapper', version: '1.13-mikroskeem'
}

build.dependsOn buildOrionLauncher

// Launcher info
applicationName = "OrionLauncher"
mainClassName = "eu.mikroskeem.orion.launcher.Bootstrap"